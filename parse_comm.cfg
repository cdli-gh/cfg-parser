# # use in NLTK
# Grammar, partially based on Jaworski (2009), adapted to existing pre-annotation

# pre: split before numbers and selected keywords

# fallback rules for out of vocabulary words (using UNKNOWN as RHS)
FRAG -> UNKNOWN | FRAG UNKNOWN
FRAG -> FRAG PARSE


###############
# Commodities #
###############
# extensions motivated by real data, without grounding in Jaworski
NUMBERS -> NUMBER | NUMBERS NUMBER


PRODUCT -> PRODUCT_NAME | PRODUCT PRODUCT_PARAM

# P106438:
# (FRAG
  # (NUMBER 1(disz))
  # (PRODUCT_NAME gu4)
  # (PRODUCT_NAME u2)
# )
# this may be an apposition or an implicit conjunction
# in this case, it is actually a PRODUCT_PARAM, because u2 means "(fed on) grass"
# either way, it should become an appos in the end
PRODUCT -> PRODUCT PRODUCT_NAME

NUMBER_PRODUCT -> NUMBERS PRODUCT | NUMBERS UNIT PRODUCT | NUMBERS PRODUCT UNIT
NUMBER_PRODUCT -> NUMBERS UNIT
# implicit product:
	# # 8(disz) sila3 2(disz) gin2 i-di3-a                            # 8(disz) sila3 2(disz) gin2 i-di3-a
	# (FRAG                                                           (FRAG
	  # (NUMBER 8(disz))                                                (NUMBER 8(disz))
	  # (UNIT sila3)                                                    (UNIT sila3)
	  # (NUMBER 2(disz))                                                (NUMBER 2(disz))
	  # (PRODUCT_NAME|UNIT gin2)                                        (PRODUCT_NAME|UNIT gin2)
	  # (PN i-di3-a)                                                    (PN i-di3-a)
	# )                                                               )


NUMBER_PRODUCT_LIST -> NUMBER_PRODUCT | NUMBER_PRODUCT_LIST NUMBER_PRODUCT 
NUMBER_PRODUCT_LIST -> NUMBER_PRODUCT_LIST

# la2 is actually minus, e.g., on P106438
# Jagersma (nd, ex. 75) https://openaccess.leidenuniv.nl/bitstream/handle/1887/16107/Binnenwerk-jagersma.pdf?sequence=2
NUMBERS -> NUMBER | NUMBERS MINUS NUMBER

##########################
# People and their roles #
##########################

PERSON -> PN | PN JOB | JOB | PERSON NATIONALITY | PERSON FILIATION

# conjunction
CONJ -> "u3"
PERSON -> PERSON CONJ PERSON
	# (FRAG                                                           (FRAG
	  # (NUMBERS (NUMBER 8(disz)))                                  |   (NUMBER 8(disz))
	  # (UNIT sila3)                                                    (UNIT sila3)
	  # (TRANSACTION                                                |   (NUMBER 4(disz))
		# (NUMBER_PRODUCT_LIST                                      |   (PRODUCT_NAME|UNIT gin2)
		  # (NUMBER_PRODUCT                                         |   (PN tab-be-li2)
			# (NUMBERS (NUMBER 4(disz)))                            |   (CCONJ u3)
			# (PRODUCT (PRODUCT_NAME gin2))))                       |   (PN dan-num2)
		# (PERSON (PN tab-be-li2)))                                 | )
	  # (UNKNOWN u3)                                                <
	  # (PERSON (PN dan-num2)))                                     <

JOB -> JOBNAME | JOB GOD | JOB CITY

GOD -> GOD CITY | GOD NATIONALITY

FILIATION -> FILIATION_EXP PERSON

##########
# Places #
##########

# "at the heart of" = "in"
SZA3 -> "sza3" 
CITY -> SZA3 CITY

# NB: sza3 (szag, "heart") in the phrase szag bala'a (http://oracc.museum.upenn.edu/epsd2/o0038593) is an accounting term, bala'a (spelled bala or bala-a) is in L1 or GEN.L1; not to be confused with bala "tax"

###############
# Transaction #
###############

# szu-ba-ti transactions:
# Archi and Pomponio, "szu-ba-ti" text structure, cf. Hayes p. 372
# animali ba-ug7
# ki-NP-ta
# NP szu-ba-ti
NUMBER_PRODUCT_LIST -> NUMBER_PRODUCT_LIST DEAD

# szu-ba-ti transactions:
# Archi and Pomponio, "szu-ba-ti" text structure, cf. Hayes p. 372
# animali ba-ug7
# ki-NP-ta
# NP szu-ba-ti
TRANSACTION -> NUMBER_PRODUCT_LIST LOCATION | TRANSACTION LOCATION
TRANSACTION -> NUMBER_PRODUCT_LIST AGENT | TRANSACTION AGENT
TRANSACTION -> NUMBER_PRODUCT_LIST DATE | TRANSACTION DATE

# Transaction :=
	# NumberAnimal God kiszib3.PN bala.PN mu.PN_SZE3 PN.maszkim mu-DU.PN giri3.PN Receiver Supplier Day Month Year (order uncertain)
	# move day month year to DATED_TRANSACTION
# P123831
TRANSACTION -> NUMBER_PRODUCT_LIST AGENT

# generalization for lists for other than animals
TRANSACTION -> TRANSACTION LOCATION

# extended
TRANSACTION -> NUMBER_PRODUCT_LIST PURPOSE 

TRANSACTION -> TRANSACTION AGENT
	# adds infinite number of AGENT roles
	# Jaworski: TRANSACTION -> NUMBER_PRODUCT_LIST PERSON MUDU_PERSON 
	# actually followed by the verb "received", but we could just strip that off
	# P106438:
	# 219	3(disz)	3(disz)[one]	NUM	NU	_	220	nummod	_	_
	# 220	sila4	sila[lamb]	NOUN	N	Number=Sing	0	_	_	_
	# 221	2(u)	2(u)[ten]	NUM	NU	_	223	nummod	_	_
	# 222	1(disz)	1(disz)[one]	NUM	NU	_	221	nummod	_	_
	# 223	masz2	masz[goat]	NOUN	N	Number=Sing	220	appos	_	_
	# 224	mu-kux(DU)	n]	VERB	V	Number=Sing|Person=3	223	amod	_	_
	# 225	{d}szara2	Szara[1]	PROPN	DN	Animacy=Hum|Number=Sing	220	appos	_	_
	# 226	|KI.AN|{ki}	Ki'an[1]	PROPN	SN	Animacy=Hum|Case=Gen|Number=Sing	225	GEN	_	_

# unspecified agent, grammatical interpretation in terms of order
# ERG > ABS
AGENT -> PERSON
	
# bala.PN := "bala" Person
# interpretation: bala "tax" + PN.GEN
AGENT -> BALA PERSON 

# mu.PN-sze3 := "mu" Person "-sze3"
# person in whose name the receiver or supplier acted
# interpretation: mu "name" + PN[.GEN].TERM (terminative indicates goal)
# i.e. "for the name of PERSON"
# note: we do not check the terminative case here!
AGENT -> MU PERSON

# PN.maszkim := Person "maszkim"
# overseer of the transaction
# interpretation: maszkim "administrator" + PN.ABS, i.e., nmod (epithet) of PN.ABS
AGENT -> PERSON MASZKIM

# "from PERSON"
AGENT -> KI PERSON
# # ki ab-ba-sa6-ga-ta "from Abbasaga" (P102335), but also ki ab-ba-sa6-ga (P106228, without case marking)
# (FRAG
  # (KI ki)
  # (PN ab-ba-sa6-ga-ta)
# )

# marking place names in szu-ba-ti texts
LOCATION -> KI CITY

# Jaworski: TRANSACTION -> NUMBER_PRODUCT_LIST GIRI3_PERSON
# giri3.PN := "giri3" Person
# middleman between supplier and receiver
# interpretation: giri3 "foot" + PN.GEN (can be implicit)
# however, giri3 can also be a preposition "via, by means of, under the authority of someone", and according to EPSD, this is the primary meaning
# accordingly, interpret (here) as giri3 -mark-> PN
AGENT -> GIRI3 PERSON

# mu-DU.PN := "mu-DU" Person
# person on whose account the transaction took place, receiver was his representative
# interpretation: mu-kux(DU) "delivery" + PN.GEN (can be implicit)
AGENT -> MUDU PERSON | MUDU GOD
	# Jaworski: TRANSACTION -> NUMBER_PRODUCT_LIST MUDU_PERSON
	# P106438:
	# 227	6(disz)	6(disz)[one]	NUM	NU	_	228	nummod	_	_
	# 228	udu	udu[sheep]	NOUN	N	Number=Sing	0	_	_	_
	# 229	bar-gal2	bargal[sheep_designation]	VERB	V	_	228	amod	_	_
	# 230	4(u)	4(u)[ten]	NUM	NU	_	232	nummod	_	_
	# 231	2(disz)	2(disz)[one]	NUM	NU	_	230	nummod	_	_
	# 232	udu	udu[sheep]	NOUN	N	Number=Sing	0	_	_	_
	# 233	bar-su-ga	barsuga[sheep_designation]	VERB	V	_	232	amod	_	_
	# 234	2(disz)	2(disz)[one]	NUM	NU	_	235	nummod	_	_
	# 235	masz2	masz[goat]	NOUN	N	Number=Sing	0	_	_	_
	# 236	giri3	giri[foot]	NOUN	N	Number=Sing	0	_	_	_
	# 237	ur-e11-e	Ure'e[1]	PROPN	PN	Animacy=Hum|Case=Gen|Number=Sing	236	GEN	_	_

# kiszib3.PN := "kiszib3" Person
# person who sealed the document	
# interpretation: kiszib3 "seal(ed tablet)" + PN.GEN
AGENT -> KISZIB PERSON

# note that NOUN is not defined here, but must be inferred from UD(!) syntax
PURPOSE -> NOUN GOD | NOUN GOD CITY
	#  P109483:
	# 1	1(disz)	1
	# 2	gu4	bull
	# 3	niga	barley-fattened
	# 4	ma2	for the ship
	# 5	an-na	of An
	# 6	sza3	in
	# 7	unu{ki}-ga	Uruk
	# 8	giri3	the transmitter was
	# 9	bar-bar-re	B.
	# 10	zi-ga	disembursement from
	# 11	be-li2-du10	B.
	# 12	iti	month of
	# 13	a2-ki-ti	Akiti
	# 14	mu	year
	# 15	an-sza-an{ki}	when Anshan
	# 16	ba-hul	was destroyed

PURPOSE -> GOD | GOD CITY
	# # 1(disz) udu niga e2 {d}na-na-a                                # 1(disz) udu niga e2 {d}na-na-a
	# (FRAG                                                           (FRAG
	  # (NUMBER 1(disz))                                                (NUMBER 1(disz))
	  # (PRODUCT_NAME udu)                                              (PRODUCT_NAME udu)
	  # (PRODUCT_PARAM niga)                                            (PRODUCT_PARAM niga)
	  # (PRODUCT_NAME e2)                                               (PRODUCT_NAME e2)
	  # (GOD_NAME {d}na-na-a)                                           (GOD_NAME {d}na-na-a)
	# )                                                               )

# extended
TRANSACTION -> TRANSACTION ACCOUNTING

#extended
ACCOUNTING -> ZIGA PERSON

# DATED_TRANSACTION to make sure "RECEIVED" is the last element
# szu-ba-ti transactions:
# Archi and Pomponio, "szu-ba-ti" text structure, cf. Hayes p. 372
# animali ba-ug7
# ki-NP-ta
# NP szu-ba-ti
DATED_TRANSACTION -> TRANSACTION RECEIVED | TRANSACTION DATE | TRANSACTION DATE RECEIVED | TRANSACTION RECEIVED DATE

########################
# Transaction Epilogue #
########################

# don't add kiszib3-ba to the KISZIB rule, this is plural, but in summary only (gaba-ri kiszib3-ba ur-{d}suen "copy.ABS [ sealed_tablets [ Ursuen.GEN].GEN )
SUMMARY -> SZU_NIGIN | COPY 
SUMMARY -> SUMMARY SZU_NIGIN | SUMMARY COPY | SUMMARY DATE
# | SZU_LAGAB
COPY -> GABARI  | GABARI KISZIB PERSON | GABARI KISZIBA PERSON

# totals, in summary
# Szu.Nigin := NumberAnimal
SZU_NIGIN ->  SZUNIGIN NUMBER_PRODUCT_LIST
SZUNIGIN -> "szunigin"

# no example for szu lagab in gold corpus, but lagab means "debt" and szu seems to mean something like "collection", here (lit. "hand, claw")
# Summary := Szu.Lagab Szu.Nigin Number
# Szu.Lagab := NumberAnimal

# artificial root node for end of tablets
EPILOG -> TRANSACTION SUMMARY | DATED_TRANSACTION SUMMARY | TRANSACTION NUMBERS | DATED_TRANSACTION NUMBERS

########
# Date #
########

# date
# MONTH YEAR
DATE -> DAY MONTH YEAR | MONTH YEAR | DAY MONTH | DAY | MONTH

# day
DAY -> U4 NUMBERS

# month 
MONTH -> ITI MONTH_NAME

YEAR -> MU PERSON PERSON VERB | MU PERSON CITY VERB | MU CITY VERB | MU PERSON NOUN VERB
# problem is a year could just be any phrase, maybe just take all until the next verb

YEAR -> MU  PERSON NOUN GOD VERB
  # (DATE (MONTH (ITI iti) (MONTH_NAME e2-iti-6(disz))))        |   (MONTH_NAME e2-iti-6(disz))
  # (MU mu)                                                         (MU mu)
  # (UNKNOWN {d}szu-{d}suen)                                    |   (PN {d}szu-{d}suen)
  # (AGENT (PERSON (JOB (JOBNAME lugal))))                      |   (JOBNAME|PRODUCT_PARAM lugal)
  # (NATIONALITY uri5{ki}-ma-ke4)                               |   (CITY|NATIONALITY uri5{ki}-ma-ke4)
  # (UNKNOWN na-ru2-a-mah)                                      |   (NOUN na-ru2-a-mah)
  # (GOD (GOD_NAME {d}en-lil2-ra))                              |   (GOD_NAME {d}en-lil2-ra)
  # (UNKNOWN mu-na-du3))                                        |   (VERB mu-na-du3)

YEAR -> MU CITY
  # (PRODUCT_PARAM|U4 u4)                                           (PRODUCT_PARAM|U4 u4)
  # (NUMBER 4(disz)-kam)                                            (NUMBER 4(disz)-kam)
  # (ITI iti)                                                       (ITI iti)
  # (MONTH_NAME e2-iti-6(disz))                                     (MONTH_NAME e2-iti-6(disz))
  # (MU mu)                                                         (MU mu)
  # (CITY hu-uh2)                                                   (CITY hu-uh2)

YEAR -> MU  NOUN GOD VERB
  # (PRODUCT (PRODUCT_NAME naga) (PRODUCT_PARAM u4))            |   (NUMBER 2(u))
  # (NUMBERS (NUMBER 2(u)))                                     |   (NUMBER 8(disz)-kam)
  # (NUMBERS (NUMBER 8(disz)-kam))                              |   (ITI iti)
  # (DATE (MONTH (ITI iti) (MONTH_NAME dal)))                   |   (MONTH_NAME dal)
  # (MU mu)                                                         (MU mu)
  # (UNKNOWN ma2)                                               |   (NOUN ma2)
  # (GOD (GOD_NAME {d}en-ki))                                   |   (GOD_NAME {d}en-ki)
  # (UNKNOWN ba-ab-du8))                                        |   (VERB ba-ab-du8)

##################
# pre-annotation #
##################

# preannotation (COMM)
PRODUCT_NAME -> "COM"
PRODUCT_PARAM -> "MOD"
NUMBER -> "COUNT"

# preannotation (POS)
NUMBER -> "NU"
PN -> "PN" | "RN"
NATIONALITY -> "SN" | "AN" | "EN" | "GN" 
FILIATION -> "LN"	
GOD -> "CN"	| "DN"
CITY -> "SN" | "AN" | "GN" | "QN" | "TN" | "FN"

# NER
# AN 	Agricultural (locus) Name 
# CN 	Celestial Name 
# DN 	Divine Name 
# EN 	Ethnos Name 
# FN 	Field Name 
# GN 	geographical name
# LN 	Line Name (ancestral clan) 
# MN 	Month Name 
# ON
# PN 	Personal Name 
# QN 	Quarter Name (city area) 
# RN 	royal name
# SN 	settlement name
# TN 	temple name
# WN 	watercourse name
# YN 	Year Name

